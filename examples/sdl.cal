# Compile with:
# cac examples/sdl.cal -i std -bo use-libc -l SDL2

include "cores/select.cal"
include "std/io.cal"

const SDL_INIT_VIDEO 32
const SDL_RENDERER_ACCELERATED 2
const SDL_RENDERER_PRESENTVSYNC 4
const SDL_QUIT 0x100

extern C i32  SDL_Init u32 end
extern C addr SDL_CreateWindow addr i32 i32 i32 i32 u32 end
extern C addr SDL_CreateRenderer addr i32 u32 end
extern C i32  SDL_PollEvent addr end
extern C void SDL_DestroyWindow addr end
extern C void SDL_DestroyRenderer addr end
extern C void SDL_Quit end

struct SDL_Event
	array 56 u8 pad
end

let addr window
let addr renderer

"Init " printstr
SDL_INIT_VIDEO SDL_Init printdec new_line

"Window " printstr
"Callisto\0" Array.elements + @ 0 0 320 240 0 SDL_CreateWindow -> window
window printdec new_line

"Renderer " printstr
window -1 SDL_RENDERER_PRESENTVSYNC SDL_RENDERER_ACCELERATED or SDL_CreateRenderer -> renderer
renderer printdec new_line

while true do
	let SDL_Event e
	while &e SDL_PollEvent do
		if &e d@ SDL_QUIT = then
			"Bye!\n" printstr
			renderer SDL_DestroyRenderer
			window SDL_DestroyWindow
			SDL_Quit
			0 exit
		end
	end
end
